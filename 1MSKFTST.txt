 CLS:CLEAR:GFILL 0,0,255,455,0

 GOSUB @INITFONT
'-----
 LOCATE 0,16:POSX=0:POSY=0
 PAL=15:SCALE=1
 RESTORE @TXTDATA
@MAIN
 READ TEXTDATA$:IF TEXTDATA$=="_" THEN @QUIT
 GOSUB @DRAWTEXTH
 GOTO @MAIN
@QUIT
 LINPUT A$
 END
'-----
@TXTDATA
DATA "242221442422246A244E245E245E21213A232121352F24332443243F3B7624724F432439243C212A"
DATA "2158242A246C244F455B244E41302447332C434A24724550244324462424243F2448"
DATA "3B572443243F246924242444244E245E244B242B395F246A24462424243F2159"
DATA ""
DATA "244A21442121323F2472384024432446246B244E242B2121246F242B2469244D213C24483B572426242C"
DATA "242A246C2462323F24722435246C243F244E242B246F242B2469244A242B2443243F2144"
DATA ""
DATA "462C242C24492426244B242B244A246A243D242624402443243F2144"
DATA ""
DATA "3A454C323D5124402448242B443625392554213C254924402448242B"
DATA "243D2473244A254125632541244A246224732438246324222121434724382446244D2428"
DATA ""
DATA "2462244324483632246D243724242462244E244E4A524E5A24724C23246F2443243F243C2144"
DATA ""
DATA ""
DATA "423339542439246B244B244F323F242B252D213C2472322124372446242F244024352424212521252125"
DATA "_"

'====================
' 2 ¼Ý½³ ÍÝ¶Ý
'====================
@NUM2BIN
'INPUT : NUM  // 2¼Ý½³ Æ ½Ù¶½Þ
'OUTPUT: BIN$ // 2¼Ý½³ Æ ÍÝ¶Ý ¼À ¶½Þ
 N=NUM:R=0:BIN$=""
'-----
@NUM2BINLOOP
 IF N==0 THEN RETURN
 R=N%2:N=FLOOR(N/2):BIN$=STR$(R)+BIN$
GOTO @NUM2BINLOOP

'====================
' ÆÎÝºÞ Ë®³¼Þ Ö³
'====================
@INITFONT
 ROW=0:CELL=0:PAGE=0 ' FOR JIS CODE
 INDEX=0
 FONTDATA$="" ' FONT DATA
 CHRDATA$=""
 POSX=0:POSY=0
 BASEPOSX=0:BASEPOSY=0
 CHRINDEX=0 ' ·¬×¸À ÊÞÝºÞ³
 SUBINDEX=0 ' ·¬×¸À Å² É ²ÝÃÞ¯¸½(0~4)
 
 FONTCHRPREFIX$="SPU"
 FONTSTARTPAGE=4 ' 0~4
 FONTFILENAME$="MSKFNT"
 _T=FONTSTARTPAGE+3
 FONTTMPCHR$=FONTCHRPREFIX$+STR$(_T)
 FONTTMPCHINDEX=232
 SPPAGE 0
 GOSUB @READFONTDATA
 RETURN
 
'=====
@DRAWFONTCHR
' INPUT : POSX, POSY, PAL, SCALE, ROW, CELL
 GOSUB @WRITETMPFONT
' GFILL POSX,POSY,POSX+7,POSY+7,0
 GPUTCHR POSX,POSY,FONTTMPCHR$,FONTTMPCHINDEX,PAL,SCALE
 RETURN
'=====
@DRAWFONTCHR2
' INPUT : POSX, POSY, PAL, SCALE, JISCODE$
 GOSUB @JCODE2CODEPOINT
 GOSUB @DRAWFONTCHR
 RETURN

@JCODE2CODEPOINT
' INPUT : JISCODE$
' OUTPUT: ROW, CELL
  _T$=MID$(JISCODE$,0,2):ROW=VAL("&H"+_T$)-32
  _T$=MID$(JISCODE$,2,2):CELL=VAL("&H"+_T$)-32
 RETURN
'=====
@DRAWTEXTH
' INPUT : POSX, POSY, PAL, SCALE, TEXTDATA$
 _L=FLOOR(LEN(TEXTDATA$)/4)-1
 BASEPOSX=POSX
 FOR DRAWTEXT_I=0 TO _L
  _I=DRAWTEXT_I
  JISCODE$=MID$(TEXTDATA$,_I*4,4)
  IF JISCODE$=="0D0A" THEN GOSUB @NEWLINEPOS:BASEPOSY=POSY+(SCALE*8):GOSUB @DRAWTEXTSKIP
  GOSUB @JCODE2CODEPOINT
  GOSUB @DRAWFONTCHR2
  POSX=POSX+(SCALE*8)
@DRAWTEXTSKIP
 NEXT
 GOSUB @NEWLINEPOS
 RETURN
'=====
@NEWLINEPOS
 POSX=BASEPOSX
 BASEPOSY=BASEPOSY+(SCALE*8)
 POSY=BASEPOSY
 RETURN
'=====
@GETFONTDATA
' INPUT : ROW, CELL // JIS CODE POINT
' OUTPUT: FONTDATA$ // FONT DATA
 FONTDATA$=""
 GOSUB @CODEPOINT2INDEX
 GOSUB @GETFONTCHRNAME
 CHRREAD(FONTCHRNAME$,CHRINDEX),_D$
 _D$=MID$(_D$,SUBINDEX*16,16):FONTDATA$=""
 FOR _I=0 TO 8
  _H$=MID$(_D$,_I*2,2)
  _V=VAL("&H"+_H$)
  NUM=_V:GOSUB @NUM2BIN:_BIN$=RIGHT$("00000000"+BIN$,8):FONTDATA$=FONTDATA$+_BIN$
 NEXT
 RETURN
'=====
@WRITETMPFONT
' Ë®³¼Þ½Ù ÀÒÆ Ì«ÝÄÃÞ°À ¦ ÃÝ¶²
' INPUT: ROW, CELL
 GOSUB @GETFONTDATA
 GOSUB @FONTDATA2CHRDATA
 CHRSET FONTTMPCHR$,FONTTMPCHINDEX,CHRDATA$
 RETURN
'=====
@FONTDATA2CHRDATA
' INPUT : FONTDATA$ // FONT DATA
' OUTPUT: CHRDATA$
 CHRDATA$=""
 FOR _I=0 TO 63:_T$=MID$(FONTDATA$,_I,1)
  IF _T$=="0" THEN CHRDATA$=CHRDATA$+"0" ELSE CHRDATA$=CHRDATA$+"F"
 NEXT
 RETURN
'=====
@INDEX2CODEPOINT
' INPUT : INDEX
' OUTPUT: ROW, CELL, PAGE
  ROW=FLOOR(INDEX/94)+1:CELL=(INDEX%94)+1:PAGE=FLOOR(PAGE/1024)
  IF (7<ROW) THEN ROW=ROW+4
  RETURN
'=====
@CODEPOINT2INDEX
' INPUT : ROW, CELL
' OUTPUT: INDEX, PAGE, CHRINDEX, SUBINDEX
  _ROW=ROW:_CELL=CELL
  IF (_ROW<1) OR (47<_ROW) THEN _ROW=2:_CELL=2:GOTO @CODEPOINT2INDEXC
  IF (8<_ROW) AND (_ROW<13) THEN _ROW=2:_CELL=2:GOTO @CODEPOINT2INDEXC
  IF (12<_ROW) THEN _ROW=_ROW-4
@CODEPOINT2INDEXC
  INDEX=((_ROW-1)*94)+(_CELL-1):PAGE=FLOOR(INDEX/1024)
  _T=INDEX%1024:CHRINDEX=FLOOR(_T/4):SUBINDEX=(_T%4)
  RETURN
'=====
@GETFONTCHRNAME
' INPUT : PAGE (0~)
' OUTPUT: FONTCHRNAME$ // FONT CHR NAME
 _T=FONTSTARTPAGE+PAGE-1
 IF _T>7 THEN FONTCHRNAME$="" ELSE FONTCHRNAME$=FONTCHRPREFIX$+STR$(_T)
 RETURN
'=====
@READFONTDATA
 _I=0
@READFONTDATAI
 _I=_I+1:IF _I>4 THEN @READFONTDATAEXIT
 PAGE=_I-1:GOSUB @GETFONTCHRNAME
 IF FONTCHRNAME$=="" THEN @READFONTDATAEXIT
 LOAD FONTCHRNAME$+":"+FONTFILENAME$+STR$(_I),FALSE
 GOTO @READFONTDATAI
@READFONTDATAEXIT
 RETURN


